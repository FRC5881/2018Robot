import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.01.11"
}

group 'org.techvalleyhigh.frc5881.powerup'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def TEAM = 5881
def ROBOT_CLASS = "org.techvalleyhigh.frc5881.powerup.robot.Robot"

// Define the targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

// Defining dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and MatchData Library. NavX listed but commented out for potential future use.
dependencies {
    compile wpilib()
    compile ctre()
    //compile navx(), bare
    compile openrio.powerup.matchData()
    compile pathfinder()
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Setting up the Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}